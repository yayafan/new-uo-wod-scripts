/////////////////////////////////////////////////////////////////////////////
//
//  merchant_dbsaver - Saves merchant database periodically
//
//          complains, via syslog and broadcast, if it can't.
//          If this script dies, mdbsaverchk should notice and complain.
//
/////////////////////////////////////////////////////////////////////////////

use os;
use uo;
use file;

include "include/utility";
include "include/client";

function savemdb();
	var output_text := {};
	var playermerchantlist := GetGlobalProperty ("#playermerchantlist");
	if (!playermerchantlist)
		output_text[1] := "Unable to find any merchants.";
      else
		var storage := FindStorageArea( "Merchant Storage" );
		if (!storage)
			output_text[1] := "Error opening merchant storage!";
		else
			var counter := 1;
			foreach merchantserial in playermerchantlist
				var merchant := SystemFindObjectBySerial (merchantserial);
				if (merchant)
					var inv_fs := FindRootItemInStorageArea (storage, merchantserial + " FS");
					if (inv_fs)
						foreach item in ListRootItemsInContainer (inv_fs)
							if ( item.amount > 1 )
								output_text[counter] := merchant.name + " at " + merchant.x + ", " + merchant.y + ", " + item.desc + " at " + item.sellprice + " each";
							else
								output_text[counter] := merchant.name + " at " + merchant.x + ", " + merchant.y + ", " + item.desc + " at " + item.sellprice;
							endif
							counter := counter+1;
						endforeach
					endif
				endif
			endforeach
		endif
	endif
	var res := WriteFile("vendors1.txt", output_text);
	return res;
endfunction

const MINUTES_BETWEEN_SAVES := 15;

print ("Saving merchant database every " + MINUTES_BETWEEN_SAVES + " minutes");

while (1)
	sleep (MINUTES_BETWEEN_SAVES * 60);
	
	syslog ("Saving Merchant Database");
	var duration := ReadGameClock() - 1;

	var res := savemdb();

	duration := ReadGameClock() - duration;


	if (res)
		SetGlobalProperty( "#:merchants_database:lastsave", ReadGameClock() );
		syslog( "Merchant Database saved (" + duration + " seconds)" );
	else
		syslog( "Error saving merchant database: " + res );
	endif

endwhile


